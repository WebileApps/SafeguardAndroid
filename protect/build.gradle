plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.webileapps.safeguard'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.scottyab:rootbeer-lib:0.1.0'
    implementation 'com.google.android.material:material:1.10.0'
    
    // Network security
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.webileapps'
                artifactId = 'safeguard'
                version = '0.2.0'
                
                from components.release

                pom {
                    name = 'Safeguard'
                    description = 'Android security library for various security checks'
                    url = 'https://github.com/WebileApps/SafeguardAndroid'
                    
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }
    }
}
